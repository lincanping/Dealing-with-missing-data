STOP
********************************************************************************
/*In case to avoid anyone who was working on other do-files trying to run the 
codes of this do-file before saving their previous displays, it is better to 
put a "STOP" at the very beginning of the codes. */
********************************************************************************


********************************************************************************
/*PREREQUISITES 

Create new directories named CODE, DATA, TEMP and WORK 
The logfiles created by Stata will be stored in the CODE folder. 
The data files downloaded will be stored in the DATA folder.
All the temporary data will be saved into the TEMP folder.
New datafiles expected will be saved into the WORK folder.

DATA: You will need waves 1 of the UK Household Longitudinal Study (UKHLS). 
Download the dataset "SN6614" from the UK Data Service
(after registering and doing a request). 
Make copies of “a_indresp.dta” under the directory DATA/UKHLS. 

NOTE: Make sure that you change the name of the pathways to the documents
 (the red bits of code below).
***************************************************************************** */


********************************************************************************
*Ⅰ. Apply preliminary settings

/*To clear all the display that could be confusing, it is always good to apply 
preliminary settings by clearing any displays, and replacing the data folders 
correctly if applicable. */

version 16		//If older version of Stata could not run, try version 16 

clear
cls
global data "D:/Desktop/Modules/Lab/QS904/DATA/"
global temp "D:/Desktop/Modules/Lab/QS904/TEMP/"
global work "D:/Desktop/Modules/Lab/QS904/WORK/"
global code "D:/Desktop/Modules/Lab/QS904/CODE/"


/*global directories settings allow further steps to use "$folder/" as shortcut
when referring to a specific directory*/
*For Mac computers, please use forward slashes in the directory.

set more off, perm
/*Tell Stata not to pause or display the more message forever. */
capture log close
/*Tell Stata to close any log files opened. */
capture log using $code/logFile_assignment2.txt, replace text
/*In order to provide a place to check the displays, create a log file and save
it into the code folder. If the file existed, replace it. */
********************************************************************************


********************************************************************************
*Ⅱ. Open the data files and explore it.

*[1]. Open the UKHLS data files and keep the specific variables selected.
/*Only these variables of the specific file will appear on the screen. 
If the variables existed, then clear them and apply the new ones.*/

use "$data/UKHLS/a_indresp", clear
keep pidp a_dvage a_sex a_racel a_fimngrs_dv ///
     a_qfhigh_dv a_paedqf a_maedqf ///
     a_indpxus_xw a_strata a_psu

	 
*[2]. Explore the key variables and the structures of the dataset.
codebook, compact
labelbook a_dvage a_sex a_racel a_qfhigh_dv a_paedqf a_maedqf a_fimngrs_dv
describe 

/*Add the assigned values to the categories and check whether there are some unexpected values.*/
numlabel _all, add	

tab1  a_dvage a_sex a_racel a_qfhigh_dv a_paedqf a_maedqf


*[3]. Declare the missing values.
*the former outputs of tab command shows the range of redundant values.
mvdecode _all, mv (-9/-1 = . )
mvdecode a_racel a_qfhigh_dv a_paedqf a_maedqf, mv (96/97 = . )

codebook, compact
/*Missing values appear in racel, respondents' and parents' educational qualifications.*/

tab1 a_dvage a_sex a_racel a_qfhigh_dv 
tab1 a_paedqf a_maedqf
********************************************************************************


********************************************************************************
*Ⅲ. Dealing with missing data

*	(A) Eliminate the missing values
/*We are going to create three kinds of variables for parents'qualifications.*/

/*You may be interested in how a couples' educational level (one of the them 
respectively and both of them integrally) affects a child's earning.
Let's generate new variables and categorize parents' educational backgrounds 
more intuitively.*/

*copy the variables and recode the data as follows:
capture drop motheredu
clonevar motheredu = a_maedqf	
replace motheredu = 0 if motheredu == 1
replace motheredu = 1 if motheredu == 2
replace motheredu = 2 if motheredu == 3
replace motheredu = 3 if motheredu == 4
replace motheredu = 4 if motheredu == 5

capture drop fatheredu	 
clonevar fatheredu = a_paedqf
replace fatheredu = 0 if fatheredu == 1
replace fatheredu = 1 if fatheredu == 2
replace fatheredu = 2 if fatheredu == 3
replace fatheredu = 3 if fatheredu == 4
replace fatheredu = 4 if fatheredu == 5

*Create label for the recoded values
label define mothereduL ///
0 "she did not go to school at all " ///
1 "she left school with no qualification" ///
2 "she left school with some qualifications" ///
3 "she gained post school quals or certs" ///
4 "she gained a university degree or higher degree", replace

label define fathereduL ///
0 "he did not go to school at all " ///
1 "he left school with no qualification" ///
2 "he left school with some qualifications" ///
3 "he gained post school quals or certs" ///
4 "he gained a university degree or higher degree", replace

*Assign the label categories to the variables
label values motheredu mothereduL
label values fatheredu fathereduL
numlabel mothereduL fathereduL, add

*Generate a multiple-item scale for parents' education
capture drop parentedu
gen parentedu = motheredu + fatheredu

label define parenteduL ///
0 "none of them ever went to school" ///
1 "one did not go to school, the other left school with no qualification" ///
2 "both of them left school with no qualification" ///
3 "one left school with no qualification and the other left with qualification" ///
4 "both of them left school with some qualifications" ///
5 "one left school with some qualification and the other gained post school qualification" ///
6 "both of them gained post school quals or certs" ///
7 "one gained post school quals and the other gained degree qualification" ///
8"both of them have degree qualifications", replace

label values parentedu parenteduL
numlabel parenteduL, add

*Rename the variables we just created
label variable motheredu "mother's educational qualifications(recoded)"
label variable fatheredu "father's educational qualifications(recoded)"
label variable parentedu "parents' educational qualifications(paired)"

*Check what we have got
tab1 motheredu fatheredu parentedu
********************************************************************************


********************************************************************************
*Ⅳ. Take into account the complex sample design.

*(a) Tell Stata about the design of the sample by specifying the Primary 
*Sampling Units (PSUs), the strata and the appropriate weight.

*In this case, we can use the following command: 

svyset a_psu [pweight = a_indpxus_xw], strata(a_strata) singleunit(scaled)
svydes

regress a_fimngrs_dv parentedu		//no weights, no control
svy: regress a_fimngrs_dv parentedu //using weights, no control

regress a_fimngrs_dv parentedu a_dvage a_sex a_racel a_qfhigh_dv		//no weights, control 
svy: regress a_fimngrs_dv parentedu a_dvage a_sex a_racel a_qfhigh_dv	//using weights, control
********************************************************************************


********************************************************************************
*Ⅴ. Missing patterns and missing mechanism

*[1]. Explore the patterns of missing data.
*(a) Summarize the missing values in the key variables.
misstable summarize a_dvage a_sex a_racel a_qfhigh_dv a_paedqf a_maedqf a_fimngrs_dv

*Notice that some values are missing in the race variable. While a large proportion 
*of values are missing in variables respondents' and their parents' qualifications.

*(b) Report the patterns of missing data.
tab1 a_paedqf a_maedqf a_qfhigh_dv a_racel, missing
misstable patterns a_qfhigh_dv a_paedqf a_maedqf a_racel, freq


*The output from misstable patterns indicates each pattern of missing data that 
*occurs, ordered according to the frequency of its occurrence. 
*The pattern is defined in relation to whether the variables respondents' 
*ethnicity (1) and education(2), father's education (3) 
*and mother's education (4) are observed '1' or missing '0'.


*Notice that 7381 people for which the three specified variables have been 
*observed. We can compare the missing data among the variables to see the 
*patterns. The most common missing data pattern is when respondents' race and 
*highest qualification are observed but both parents' educational qualifications 
*are missing (n=25826). In the case that all values of the three variables of 
*education are missing but respondents' ethnicity is observed ranks the 
*second common pattern. There are also some patterns of missingness with 
*smaller frequencies. 


*[2]. Exploring the missing mechanism

*1. Let's create a dichotomous/binary variable which indicates whether the
*parents' educational backgrounds are observed (1) or missing (0).

gen miss_edu = 0
replace miss_edu = 1 if (a_paedqf != .) 
replace miss_edu = 1 if (a_maedqf != .) 
tab miss_edu

label define miss_eduL 1 "Observed" 0 "Missing"
label values miss_edu missL
label variable miss_edu "Parents' education Observed"

tab1 a_paedqf a_maedqf miss_edu, m

*2. We can use a regression model to see if respondents' age, gender, race, 
*wages and educational qualifications predict whether parents' educational 
*qualifications are observed or not. As the variable that we want to predict has 
*only two categories, the suitable type of regression is the logistic regression.

*Let's find out the higher coded category of each variable, which will help us 
*interpret the coefficients.
tab1 miss_edu a_dvage a_sex a_racel a_qfhigh_dv 
codebook a_fimngrs_dv 
*The tab command for income variable would fail because of too many values, 
*but we know that nature of the income values is from low to high. 

 
*Rough relationships will be displayed as follows (without controls).
logit miss_edu a_dvage
logit miss_edu a_sex 		//significant
logit miss_edu a_racel		//significant
logit miss_edu a_fimngrs_dv	//significant
logit miss_edu a_qfhigh_dv	//significant

*Then run the logistic regression model with all control variables
logit miss_edu a_dvage a_sex a_racel a_fimngrs_dv a_qfhigh_dv 

*age, race and education are significant when controlled
*gender and gross monthly earning would have been significant predictors but the 
*relationships do not hold when controlling for all other variables (p>.05).


*3. Interpret the output and conclude over the nature of missingness.
tab1 a_dvage a_racel a_qfhigh_dv
logit miss_edu a_dvage a_sex a_racel a_fimngrs_dv a_qfhigh_dv

*We can see that age, race and the respondents' educational backgrounds are 
*significant predictors for whether parents' educational backgrounds are observed.
 
*We find that older respondents are less likely to have data on parents'
*educational backgrounds (-.002, p<.05).
*We find that respondents who are not British ethnicity (the highest coded category) 
*are less likely to have data on parents' educational backgrounds (-.02, p<.001).
*We also find that lower educational level respondents (the highest coded 
*category of qualifications) are more likely than upper educational level 
*respondents to have data on parents' educational backgrounds (-.01, p<.05).


*Moreover, we found evidence that the values in the parents' educational backgrounds
*variable are not missing completely at random (MCAR).
*i.e. they are associated with respondents' age, ethnicity and educational backgrounds. 

********************************************************************************


********************************************************************************
*Ⅵ. Close the log file and exit.

log close		
exit		
*End Syntax
********************************************************************************








